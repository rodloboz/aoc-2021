# AoC 2021 - Day 12: Passage Pathing

## Setup

```elixir
Mix.install([
  {:kino, "~> 0.4.1"}
])
```

```elixir
input = Kino.Input.textarea("Please paste your input:")
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule CaveSystem1 do
  def paths(connections) do
    graph = build_graph(connections)
    visit(graph, "start", ["start"])
  end

  defp visit(_graph, "end", current_path), do: [current_path]

  defp visit(graph, current_node, current_path) do
    graph[current_node]
    |> Enum.flat_map(fn node ->
      if Enum.member?(current_path, node) and node == String.downcase(node) do
        []
      else
        visit(graph, node, [node | current_path])
      end
    end)
  end

  defp build_graph(connections) do
    connections
    |> Enum.reduce(%{}, fn
      ["start" = src, dest], map ->
        Map.update(map, src, [dest], fn list -> [dest | list] end)

      [src, "end" = dest], map ->
        Map.update(map, src, [dest], fn list -> [dest | list] end)

      [src, dest], map ->
        Map.update(map, src, [dest], fn list -> [dest | list] end)
        |> Map.update(dest, [src], fn list -> [src | list] end)
    end)
  end
end

connections =
  input
  |> Kino.Input.read()
  |> String.split("\n", trim: true)
  |> Enum.map(&String.split(&1, "-"))

connections
|> CaveSystem1.paths()
|> Enum.count()
```

## Part 2

After reviewing the available paths, you realize you might have time to visit a single small cave twice. Specifically, big caves can be visited any number of times, a single small cave can be visited at most twice, and the remaining small caves can be visited at most once. However, the caves named `start` and `end` can only be visited exactly once each: once you leave the `start` cave, you may not return to it, and once you reach the `end` cave, the path must end immediately.

Now, the `36` possible paths through the first example above are:

```
start,A,b,A,b,A,c,A,end
start,A,b,A,b,A,end
start,A,b,A,b,end
start,A,b,A,c,A,b,A,end
start,A,b,A,c,A,b,end
start,A,b,A,c,A,c,A,end
start,A,b,A,c,A,end
start,A,b,A,end
start,A,b,d,b,A,c,A,end
start,A,b,d,b,A,end
start,A,b,d,b,end
start,A,b,end
start,A,c,A,b,A,b,A,end
start,A,c,A,b,A,b,end
start,A,c,A,b,A,c,A,end
start,A,c,A,b,A,end
start,A,c,A,b,d,b,A,end
start,A,c,A,b,d,b,end
start,A,c,A,b,end
start,A,c,A,c,A,b,A,end
start,A,c,A,c,A,b,end
start,A,c,A,c,A,end
start,A,c,A,end
start,A,end
start,b,A,b,A,c,A,end
start,b,A,b,A,end
start,b,A,b,end
start,b,A,c,A,b,A,end
start,b,A,c,A,b,end
start,b,A,c,A,c,A,end
start,b,A,c,A,end
start,b,A,end
start,b,d,b,A,c,A,end
start,b,d,b,A,end
start,b,d,b,end
start,b,end

```

The slightly larger example above now has `103` paths through it, and the even larger example now has `3509` paths through it.

Given these new rules, how many paths through this cave system are there?

```elixir
defmodule CaveSystem2 do
  def paths(connections) do
    graph = build_graph(connections)
    visit(graph, "start", [{"start", 1}])
  end

  defp visit(_graph, "end", current_path), do: [current_path]

  defp visit(graph, current_node, current_path) do
    graph[current_node]
    |> Enum.flat_map(fn node ->
      visited =
        Enum.find(current_path, fn e ->
          elem(e, 0) == node
        end)

      if is_nil(visited) or node != String.downcase(node) do
        visit(graph, node, [{node, 1} | current_path])
      else
        cond do
          node == "start" -> []
          Enum.any?(current_path, fn e -> elem(e, 1) == 2 end) -> []
          true -> visit(graph, node, [{node, 2} | current_path])
        end
      end
    end)
  end

  defp build_graph(connections) do
    connections
    |> Enum.reduce(%{}, fn
      ["start" = src, dest], map ->
        Map.update(map, src, [dest], fn list -> [dest | list] end)

      [src, "end" = dest], map ->
        Map.update(map, src, [dest], fn list -> [dest | list] end)

      [src, dest], map ->
        Map.update(map, src, [dest], fn list -> [dest | list] end)
        |> Map.update(dest, [src], fn list -> [src | list] end)
    end)
  end
end

connections
|> CaveSystem2.paths()
|> Enum.count()
```
